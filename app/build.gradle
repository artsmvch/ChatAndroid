plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def version_code = 12
def version_major = 1
def version_minor = 2
def version_patch = 2
def version_name = "${version_major}.${version_minor}.${version_patch}"

Properties localProperties = new Properties()
localProperties.load(project.file('local.properties').newDataInputStream())
println("loadedProperties=" + localProperties)

android {
    namespace 'com.chat.gpt'
    compileSdk 33

    signingConfigs {
        release {
            def configDir = getReleaseConfigDir()
            def releaseConfigs = loadProperties("$configDir/signing.properties")
            storeFile file("$configDir/${releaseConfigs['storeFile']}")
            storePassword releaseConfigs['storePassword']
            keyAlias releaseConfigs['keyAlias']
            keyPassword releaseConfigs['keyPassword']
        }
    }

    defaultConfig {
        applicationId "com.chat.bot"
        minSdk 23
        targetSdk 33
        versionCode version_code
        versionName version_name

        buildConfigField "String", "OPEN_AI_API_KEY", "\"${localProperties.getProperty('openai.apikey') ?: ""}\""
        buildConfigField "String", "ADMOB_INTERSTIAIL_AD_UNIT_ID", "\"${localProperties.getProperty('admob.interstitial_ad_unit_id') ?: ""}\""

        resValue "string", "admob_app_id", "\"${localProperties.getProperty('admob.app_id') ?: ""}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "app_name", "Chat GPT"
        }
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Chat GPT Debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

configurations {
    cleanedAnnotations
    implementation.exclude group: 'org.jetbrains' , module:'annotations'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation("com.squareup.okhttp3:okhttp:4.10.0")

    implementation project(path: ':chat')
    implementation project(path: ':utils')
    implementation project(path: ':firebase')

    implementation 'com.google.android.gms:play-services-ads:21.5.0'

    implementation platform('com.google.firebase:firebase-bom:31.2.3')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
}

if (localProperties['com.google.services.enabled'] == 'true') {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}